// does not work on oss.sonatype.org
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        OpenApiProcessor (MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.projectTitle
                description = project.projectDesc
                url = project.projectUrl

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = 'hauner'
                        name  = 'Martin Hauner'
                    }
                }

                scm {
                   url = "https://github.com/${project.projectGithubRepo}".toString ()
                }
            }

        }
    }

    repositories {
        maven {
            def releaseRepository = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            def snapshotRepository = uri("https://oss.sonatype.org/content/repositories/snapshots")
            url = project.isReleaseVersion ? releaseRepository : snapshotRepository

            credentials {
                username = publishUser
                password = publishKey
            }
        }
    }
}

//tasks.withType(Sign) {
//    onlyIf { project.isReleaseVersion }
//}

signing {
    useInMemoryPgpKeys(signKey, signPwd)

    signing {
        sign publishing.publications.OpenApiProcessor
    }
}
