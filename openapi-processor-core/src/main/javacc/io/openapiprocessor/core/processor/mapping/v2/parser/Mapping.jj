/*
 * Copyright 2022 https://github.com/openapi-processor/openapi-processor-core
 * PDX-License-Identifier: Apache-2.0
 */

options {
  STATIC = false;
}

PARSER_BEGIN(MappingParser)
package io.openapiprocessor.core.processor.mapping.v2.parser;

import java.io.ByteArrayInputStream;
import java.nio.charset.StandardCharsets;
import java.util.*;

/**
 * mapping parser
 */
public class MappingParser implements Mapping {
    private Mapping.Kind kind;
    private String sourceType;
    private String sourceFormat;
    private String annotationType;
    private LinkedHashMap<String, String> annotationParameters = new LinkedHashMap<String, String> ();
    private String targetType;
    private List<String> targetGenericTypes = new ArrayList<String> ();

    public MappingParser(String input) {
        this(new ByteArrayInputStream(input.getBytes(StandardCharsets.UTF_8)));
    }

    public Mapping.Kind getKind() {
        return kind;
    }

    public String getSourceType() {
        return sourceType;
    }

    public String getSourceFormat() {
        return sourceFormat;
    }

    public String getTargetType() {
        return targetType;
    }

    public List<String> getTargetGenericTypes() {
        return targetGenericTypes;
    }

    public String getAnnotationType() {
        return annotationType;
    }

    public LinkedHashMap<String, String> getAnnotationParameters() {
        return annotationParameters;
    }

    private String wrap(String source) {
        return "\"" + source + "\"";
    }
}

PARSER_END(MappingParser)

SKIP: {
      " "
    | "\t"
}

TOKEN: {
      < ARROW: "=>" >
    | < ANNOTATE: "@" >

    | < PARENTHESIS_OPEN: "(">
    | < PARENTHESIS_CLOSE: ")">

    | < PLAIN: "plain" >
    | < BOOLEAN: "true" | "false" >

    | < IDENTIFIER: ["a"-"z", "A"-"Z", "_"] ( ["a"-"z", "A"-"Z", "_", "0"-"9"] )* >
    | < QUALIFIED_TYPE: (<IDENTIFIER> | "{package-name}") ("." <IDENTIFIER>)* >

    | < FORMAT: ["a"-"z", "A"-"Z", "_"] ( ["a"-"z", "A"-"Z", "_", "-", "0"-"9"] )* >
    | < MIME_TYPE: ["a"-"z", "A"-"Z", "_"] ( ["a"-"z", "A"-"Z", "_", "-", ".", "0"-"9"] )* >
    | < MIME_SUBTYPE: ["a"-"z", "A"-"Z", "_"] ( ["a"-"z", "A"-"Z", "_", "-", ".", "0"-"9"] )* >
    | < CONTENT_TYPE: <MIME_TYPE> "/" <MIME_SUBTYPE> >

    // parameter: string
    | < STRING: "\"" ( ~["\"", "\\"] | "\\" ["\t", "\\", "\""] )* "\"" >

    // parameter: "any" number format, just used to parse the parameters list tokens
    | < NUMBER: (["a"-"f", "A"-"F", "l", "L", "x", "X", "0"-"9", ".", "_"])+ >
}


// root
Mapping mapping(): {
} {
      LOOKAHEAD(2) map() { return this; }
    | LOOKAHEAD(2) annotate() { return this; }
}

Mapping mappingType(): {
} {
    type() { return this; }
}

Mapping mappingContentType(): {
} {
    content() { return this; }
}

// type
void type(): {
} {
    (plain() | targetType()) <EOF> { kind = Mapping.Kind.TYPE; }
}

// type mapping
void map(): {
} {
    sourceType() <ARROW> targetType() <EOF> { kind = Mapping.Kind.MAP; }
}

// type annotation
void annotate(): {
} {
    sourceType() <ANNOTATE> annotationType() <EOF> { kind = Mapping.Kind.ANNOTATE; }
}

void content(): {
} {
    contentType() <ARROW> targetType() <EOF> { kind = Mapping.Kind.MAP; }
}

void plain(): {
} {
  <PLAIN> { targetType = token.image; }
}

void sourceType(): {
} {
    sourceIdentifier() { sourceType = token.image; } (":" formatIdentifier() { sourceFormat = token.image; })?
}

void contentType(): {
} {
    <CONTENT_TYPE> { sourceType = token.image; }
}

void sourceIdentifier(): {
} {
  <IDENTIFIER> | <STRING>
}

void formatIdentifier(): {
} {
  <IDENTIFIER> | <FORMAT> | <STRING>
}

void targetType(): {
} {
    LOOKAHEAD(2) annotationType() qualifiedType()
  | LOOKAHEAD(2) qualifiedType()
}

void qualifiedType(): {
} {
    <QUALIFIED_TYPE> { targetType = token.image; }
    ("<" genericParameters() ">")?
}

void annotationType(): {
} {
    <QUALIFIED_TYPE> { annotationType = token.image; }
    ("(" annotationParameters() ")")?
}

void annotationParameters(): {
} {
    LOOKAHEAD(2) annotationParameterNamed() ("," annotationParameterNamed())? | annotationParameterUnnamed()
}

void annotationParameterUnnamed(): {
    StringBuilder s = new StringBuilder();
} {
    <IDENTIFIER> { annotationParameters.put ("", token.image); }
  | <BOOLEAN> { annotationParameters.put ("", token.image); }
  | <STRING> { annotationParameters.put ("", token.image); }
  | <NUMBER> { annotationParameters.put ("", token.image); }
}

void annotationParameterNamed(): {
    Token name;
    Token value;
    StringBuilder s = new StringBuilder();
} {
    name = <IDENTIFIER> "=" (
        value = <IDENTIFIER> { annotationParameters.put (name.image, value.image); }
      | value = <BOOLEAN> { annotationParameters.put (name.image, value.image); }
      | value = <STRING> { annotationParameters.put (name.image, value.image); }
      | value = <NUMBER> { annotationParameters.put (name.image, token.image); }
   )
}

void genericParameters(): {
} {
    <QUALIFIED_TYPE> { targetGenericTypes.add (token.image); } (
        "," <QUALIFIED_TYPE> { targetGenericTypes.add (token.image); }
    )?
}
