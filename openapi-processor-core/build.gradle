plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
    id "com.github.ben-manes.versions" version "0.27.0"
}

group 'com.github.hauner.openapi'
version '1.0.0.M5'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    generatrApiVersion = '1.0.0.M3'

    bintrayUser = project.hasProperty ('BINTRAY_USER') ? BINTRAY_USER : 'n/a'
    bintrayKey =  project.hasProperty ('BINTRAY_KEY')  ? BINTRAY_KEY  : 'n/a'
}


repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url  "https://dl.bintray.com/hauner/openapi-generatr"
    }
}

test {
    useJUnitPlatform()
}

testSets {
    testInt
}

check.dependsOn testInt

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.0'
    implementation 'io.swagger.parser.v3:swagger-parser:2.0.17'
    implementation 'com.google.googlejavaformat:google-java-format:1.7'
    compileOnly "com.github.hauner.openapi:openapi-generatr-api:$generatrApiVersion"

    testImplementation ('org.spockframework:spock-core:2.0-M2-groovy-3.0') {
        exclude group: 'org.codehaus.groovy' // avoid conflicts with explicit groovy version
    }
    testImplementation 'org.spockframework:spock-junit4:2.0-M2-groovy-3.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.9.13'

    testIntImplementation "com.github.hauner.openapi:openapi-generatr-api:$generatrApiVersion"
    testIntImplementation 'io.github.java-diff-utils:java-diff-utils:4.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['GeneratrSpring']

    pkg {
        repo = 'openapi-generatr'
        name = 'openapi-generatr-spring'
        //userOrg = 'openapi-generatr'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hauner/openapi-generatr-spring'

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        GeneratrSpring (MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
