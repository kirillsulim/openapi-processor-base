plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
    id "com.github.ben-manes.versions" version "0.27.0"
}

group 'com.github.hauner.openapi'
version '1.0.0.M11'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    processorApiVersion = '1.0.0.M4'

    bintrayUser = project.hasProperty ('BINTRAY_USER') ? BINTRAY_USER : 'n/a'
    bintrayKey =  project.hasProperty ('BINTRAY_KEY')  ? BINTRAY_KEY  : 'n/a'
}


repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url  "https://dl.bintray.com/hauner/openapi-processor"
    }
}

test {
    useJUnitPlatform()
}

testSets {
    testInt
}

check.dependsOn testInt


compileGroovy {
    dependsOn 'compileKotlin'
    classpath += files(compileKotlin.destinationDir)
}

compileTestGroovy {
    dependsOn 'compileKotlin'
    classpath += files(compileKotlin.destinationDir)
}


dependencies {
    implementation 'org.codehaus.groovy:groovy:2.5.4'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'io.swagger.parser.v3:swagger-parser:2.0.17'
    implementation 'org.openapi4j:openapi-parser:0.7'
    implementation 'com.google.googlejavaformat:google-java-format:1.7'
    compileOnly "com.github.hauner.openapi:openapi-processor-api:$processorApiVersion"

    testImplementation ('org.spockframework:spock-core:2.0-M2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation ('org.spockframework:spock-junit4:2.0-M2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.10.7'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testIntImplementation "com.github.hauner.openapi:openapi-processor-api:$processorApiVersion"
    testIntImplementation 'io.github.java-diff-utils:java-diff-utils:4.5'
//    testIntRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30'
//    testIntRuntimeOnly 'org.slf4j:slf4j-nop:1.6.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set ('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: [groovydoc, dokka]) {
    archiveClassifier.set ('javadoc')
    from "$buildDir/docs"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

groovydoc {
    destinationDir file("$buildDir/docs/groovy")
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs/kotlin"
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['processor']

    pkg {
        repo = 'openapi-processor'
        name = 'openapi-processor-spring'
        //userOrg = 'openapi-generatr'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hauner/openapi-processor-spring'

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        processor (MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
