import org.gradle.api.DefaultTask
import org.gradle.api.provider.Property
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.Internal
import org.gradle.api.tasks.OutputDirectory
import org.gradle.api.tasks.TaskAction

import java.nio.file.Files
import java.nio.file.Path
import java.time.Instant

/**
 * simple task to create a Version class.
 */
abstract class VersionTask extends DefaultTask {

    /**
     * package name of the generated version class.
     */

    @Input
    abstract Property<String> getTargetPackage();

    /**
     * Target directory for the generated version class.
     *
     * Used by gradle for the up-to-date check.
     */
    @OutputDirectory
    String targetDir


    @Internal
    String version

    /**
     * generate the version class.
     */
    @TaskAction
    void generateVersion () {
        String[] parts = targetPackage.get ().split ("\\.")
        def path = Path.of (targetDir, "version", *parts)
        Files.createDirectories(path)

        def target = path.resolve ("Version.java")

        target.text = """\
/*
 * DO NOT MODIFY - this file was auto generated by buildSrc/src/main/groovy/VersionPlugin.groovy
 *
 * ${Instant.now ().toString ()}
 */

package ${targetPackage.get ()};
 
public class Version {
    public static final String version = "${version}"; 
}
 
"""
    }

}
